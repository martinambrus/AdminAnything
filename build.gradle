plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.0.0'
}

group 'com.martinambrus.adminAnything'
version '2.4.12'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

ext {
    spigotVersion = '1.16.5-R0.1-SNAPSHOT'
    vaultVersion = '1.7'
    protocollibVersion = '4.5.1'
    bstatsVersion = '1.4'
    javaassistVersion = '3.27.0-GA'
}

repositories {
    mavenCentral()
    maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
    maven { url 'https://repo.dmulloy2.net/content/groups/public/' }
    maven { url 'https://repo.codemc.org/repository/maven-public' }
    maven { url 'https://repo.md-5.net/content/repositories/releases/' }
}

dependencies {
    compileOnly "org.spigotmc:spigot-api:${spigotVersion}"
    compileOnly "com.comphenix.protocol:ProtocolLib:${protocollibVersion}"
    compileOnly "net.milkbowl.vault:VaultAPI:${vaultVersion}"
    compileOnly "org.javassist:javassist:${javaassistVersion}"
    implementation "org.bstats:bstats-bukkit:${bstatsVersion}"

    dependencies {
        compileOnly fileTree('libs')
    }
}

shadowJar {
    archiveFileName = 'adminAnything.jar'
    relocate 'org.bstats', 'com.martinambrus.adminAnything.bstats'
    includeEmptyDirs = false
}

artifacts {
    archives shadowJar
}

processResources {
    filesMatching('plugin.yml') {
        expand 'pluginVersion': project.version
    }
}

task generateAPIDocs(type: Javadoc) {
    source = sourceSets.main.allJava
    classpath = sourceSets.main.runtimeClasspath
    exclude '**/sun', '**/com.sun'
}

tasks.withType(Javadoc) {
    options.addStringOption('Xdoclint:none', '-quiet')

    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}
